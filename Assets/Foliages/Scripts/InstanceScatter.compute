#pragma kernel Scatter

#define HALF_PI 1.57079632679
#define ONE_OVER_PI 0.31830988618
#define TWO_PI 6.28318530718
#define EPSILON 0.00000000000001

uint _ResultSize;
float _ResultSizeInverse;

ByteAddressBuffer _TerrainVertices;
uint _TerrainSize;
int _TerrainStride;
int _TerrainPositionOffset;
int _TerrainNormalOffset;
int _TerrainModifyOffset;

float _SlopeLower;
float _SlopeUpper;

float3 _Anchor;

float _Stretch;

AppendStructuredBuffer<float4x4> _TransformMatrices;

float3 LoadTerrainPosition(uint index) {
    return asfloat(_TerrainVertices.Load3(index * _TerrainStride + _TerrainPositionOffset));
}
float3 LoadTerrainNormal(uint index) {
    return asfloat(_TerrainVertices.Load3(index * _TerrainStride + _TerrainNormalOffset));
}
uint LoadTerrainModifyMask(uint index) {
    return _TerrainVertices.Load(index * _TerrainStride + _TerrainModifyOffset);
}

uint CalculateTerrainIndex(uint x, uint y) {
    return (y * (_TerrainSize + 1) + x);
}

void SamplePosition(float2 normalPos, out float3 positionWS, out float gradient, out uint4 mask) {
    float2 coord = normalPos * ((float)_TerrainSize - EPSILON);
    uint2 anchor = uint2(coord);
    float2 offset = coord - anchor;

    uint indexBL = CalculateTerrainIndex(anchor.x,     anchor.y);
    uint indexBR = CalculateTerrainIndex(anchor.x + 1, anchor.y);
    uint indexUL = CalculateTerrainIndex(anchor.x,     anchor.y + 1);
    uint indexUR = CalculateTerrainIndex(anchor.x + 1, anchor.y + 1);

    float3 positionBL = LoadTerrainPosition(indexBL);
    float3 positionBR = LoadTerrainPosition(indexBR);
    float3 positionUL = LoadTerrainPosition(indexUL);
    float3 positionUR = LoadTerrainPosition(indexUR);
    float3 positionB = lerp(positionBL, positionBR, offset.x);
    float3 positionU = lerp(positionUL, positionUR, offset.x);
    float3 position = lerp(positionB, positionU, offset.y);
    positionWS = position + _Anchor;

    float3 normalBL = LoadTerrainNormal(indexBL);
    float3 normalBR = LoadTerrainNormal(indexBR);
    float3 normalUL = LoadTerrainNormal(indexUL);
    float3 normalUR = LoadTerrainNormal(indexUR);
    float3 normalB = lerp(normalBL, normalBR, offset.x);
    float3 normalU = lerp(normalUL, normalUR, offset.x);
    float3 normal = lerp(normalB, normalU, offset.y);

    float3 up = float3(0.0, 1.0, 0.0);
    gradient = dot(normal, up);

    uint maskBL = LoadTerrainModifyMask(indexBL);
    uint maskUL = LoadTerrainModifyMask(indexUL);
    uint maskBR = LoadTerrainModifyMask(indexBR);
    uint maskUR = LoadTerrainModifyMask(indexUR);
    mask = uint4(maskBL, maskBR, maskUL, maskUR);
}

float _Scale;
float _MinHeight;
float _MaxHeight;
uint _InclusionMask;
uint _ExclusionMask;

float RandomRange(float2 seed, float min, float max) {
    float random = frac(sin(dot(seed, float2(12.9898, 78.233)))*43758.5453);
    return lerp(min, max, random);
}

float4x4 RotationMatrixY(float angle) {
    float s, c;
    sincos(angle, s, c);

    return float4x4
    (
        c,   0.0, s,   0.0,
        0.0, 1.0, 0.0, 0.0,
        -s,  0.0, c,   0.0,
        0.0, 0.0, 0.0, 1.0
    );
}

[numthreads(8, 8, 1)]
void Scatter(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _ResultSize || id.y >= _ResultSize) 
        return; 

    float2 normalPos = float2(id.xy) * _ResultSizeInverse;
    float2 seed = float2(id.x, id.y);

    float r = RandomRange(seed, 0.0, _ResultSizeInverse - EPSILON);
    seed.x = r;
    normalPos.x += r;

    r = RandomRange(seed, 0.0, _ResultSizeInverse - EPSILON);
    seed.y = r;
    normalPos.y += r;

    float3 worldPos;
    float gradient;
    uint4 mask;
    SamplePosition(normalPos, worldPos, gradient, mask);
    // values goes from 1 to -1 as slope input incrase
    // hence the direction of inequalities
    if (gradient > _SlopeLower || gradient < _SlopeUpper)
        return;

    float sumI = 0.0;
    if (_InclusionMask == 0)
        sumI = 1.0;
    else
    {
        if (mask.x & _InclusionMask)
            sumI += 0.25;
        if (mask.y & _InclusionMask)
            sumI += 0.25;
        if (mask.z & _InclusionMask)
            sumI += 0.25;
        if (mask.w & _InclusionMask)
            sumI += 0.25;
    }

    r = RandomRange(seed, 0.0, 1.0);
    seed.x = r;
    if (r > sumI) return;
    

    float sumE = 0.0;
    if (!(mask.x & _ExclusionMask))
        sumE += 0.25;
    if (!(mask.y & _ExclusionMask))
        sumE += 0.25;
    if (!(mask.z & _ExclusionMask))
        sumE += 0.25;
    if (!(mask.w & _ExclusionMask))
        sumE += 0.25;
    r = RandomRange(seed, 0.0, 1.0);
    seed.y = r;
    if (r >= sumE) return;

    r = RandomRange(seed, _MinHeight, _MaxHeight);
    seed.x = r;
    float _ScaleY = _Scale * r;
    float _ScaleH = _Scale * _Stretch;

    float4x4 scaleMatrix = float4x4
    (
        _ScaleH, 0.0,    0.0,     0.0,
        0.0,    _ScaleY, 0.0,     0.0,
        0.0,    0.0,     _ScaleH, 0.0,
        0.0,    0.0,     0.0,     1.0
    );

    float4x4 objectTransformMatrix = float4x4
    (
        1.0, 0.0,    0.0,     worldPos.x,
        0.0,    1.0, 0.0,     worldPos.y,
        0.0,    0.0,     1.0, worldPos.z,
        0.0,    0.0,     0.0,     1.0
    );

    r = RandomRange(seed, 0.0, TWO_PI);
    seed.y = r;
    float4x4 randomRotationMatrix = RotationMatrixY(r);

    _TransformMatrices.Append(mul(objectTransformMatrix, mul(randomRotationMatrix, scaleMatrix)));
}
